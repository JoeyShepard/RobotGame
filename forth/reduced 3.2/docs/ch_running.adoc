[quote, Charles Moore, Masterminds of Programming]
One doesn't write programs in Forth. Forth is the program.

=== Booting

Out of the box, Tali Forth boots a minimal kernel(((kernel))) to connect to the
`py65mon` (((py65mon))) simulator. By default, this stage ends with a line such
as

----
Tali Forth 2 default kernel for py65mon (18. Feb 2018)
----

When you port Tali Forth to your own hardware, you'll have to include your own
kernel (and probably should print out a different line).

Tali Forth itself boots next, and after setting up various internal
things, compiles the high level words. This causes a slight delay, depending on
the number and length of these words. As the last step, Forth should spit out a
boot string like

----
Tali Forth 2 for the 65c02
Version ALPHA 24. December 2018 
Copyright 2014-2018 Scot W. Stevenson
Tali Forth 2 comes with absolutely NO WARRANTY
Type 'bye' to exit
----

Because these are the last high-level commands Tali Forth executes, this
functions as a primitive self-test. If you have modified the high level Forth
words in either `forth_words.fs` or `user_words.fs`, the boot process might
fail with a variant of the error message "unknown word". The built-in, native
words should always work. For this `dump` (((dump))) is a built-in word -- it is very
useful for testing.

=== Command-Line History
                                               
Tali's command line includes a simple, eight-element history function. To
access the previous entries, press `CONTROL-p`, to go forward to the next
entry, press `CONTROL-n`.
 
=== Words

Tali Forth comes with the following Forth words out of the 
box:

----
order .wid drop dup swap ! @ over >r r> r@ nip rot -rot tuck , c@ c! +! execute
emit type . u. u.r .r d. d.r ud. ud.r ? false true space 0 1 2 2dup ?dup + - abs
dabs and or xor rshift lshift pick char [char] char+ chars cells cell+ here 1-
1+ 2* 2/ = <> < u< u> > 0= 0<> 0> 0< min max 2drop 2swap 2over 2! 2@ 2variable
2constant 2literal 2r@ 2r> 2>r invert negate dnegate c, bounds spaces bl
-trailing -leading /string refill accept input>r r>input unused depth key allot
create does> variable constant value to s>d d>s d- d+ erase blank fill find-name
' ['] name>int int>name name>string >body defer latestxt latestnt parse-name
parse source source-id : ; :noname compile, [ ] literal sliteral ." s" s\"
postpone immediate compile-only never-native always-native allow-native nc-limit
strip-underflow abort abort" do ?do i j loop +loop exit unloop leave recurse
quit begin again state evaluate base digit? number >number hex decimal count m*
um* * um/mod sm/rem fm/mod / /mod mod */mod */ \ move cmove> cmove pad cleave
hexstore within >in <# # #s #> hold sign output input cr page at-xy marker words
wordsize aligned align bell dump .s disasm compare search environment? find word
( .( if then else repeat until while case of endof endcase defer@ defer! is
action-of useraddr buffer: buffstatus buffblocknum blkbuffer scr blk block-write
block-write-vector block-read block-read-vector save-buffers block update buffer
empty-buffers flush load thru list block-ramdrive-init definitions wordlist
search-wordlist set-current get-current set-order get-order root-wordlist
assembler-wordlist editor-wordlist forth-wordlist only also previous >order
forth see ed cold bye
----

NOTE: This list might be outdated. To get the current list, run `words` from
inside Tali Forth.

Though the list might look unsorted, it actually reflects the priority in the
dictionary(((dictionary))), that is, which words are found first. For instance,
the native words (((native words))) -- those coded in assembler -- always start
with `drop` and end with `bye`. This is the last word that Tali will find in
the dictionary. footnote:[If you're going to quit anyway, speed can't be that
important] The words before `drop` are those that are defined in high-level
Forth. For more information on individual the words, use the `see` command.

TIP: To find out if a given word is available, you can use the sequence
`s" myword" find-name 0<>` which will return a `true` or `false` flag.

Note that the built-in words are lower case.  While Tali is not case sensitive
-- `KASUMI` is the same word as `Kasumi` (((Kasumi))) -- newly defined words will be
lowercased as they are created and entered into the dictionary.  There is a
slight speed advantage during lookup to using lowercase words (because Tali
doesn't have to lowercase the entered text), so all of the tests for Tali are
in lowercase.

                                               
==== The ANS Standard
                                   
Tali Forth is orientated on ANS Forth, the standard defined by the American
National Standards Institute (ANSI). See
https://forth-standard.org/standard/alpha for the complete list of ANS Forth
words.

==== Gforth

Tali also adopted some words from Gforth such as `bounds` or `execute-parsing`.
In practical terms, Tali aims to be a subset of Gforth: If a program runs on
Tali, it should run on Gforth the same way or have a very good reason not to.
See https://www.complang.tuwien.ac.at/forth/gforth/Docs-html/Word-Index.html for
a complete list of Gforth words.


==== Tali-Specific Words
                                               
In addition, there are words that are specific to Tali Forth.  

*0 ( +--+ 0 )* - Push the number 0 on the Data Stack. Having this as an actual
word speeds up processing because the interpreter doesn't have to convert the
character "0" into the number `0`.

*1 ( +--+ 0 )* - Push the number 1 on the Data Stack.

*2 ( +--+ 0 )* - Push the number 2 on the Data Stack.

*allow-native ( +--+ )* - Mark last word in dictionary to that it can be natively
compiled if it is less than or equal to nc-limit in size.

*always-native ( +--+ )* - Mark last word in dictionary so that it is always natively compiled.

*bell ( +--+ )* - Ring the terminal bell (ASCII 07).

*block-read ( addr blk# +--+ )* - This is a vectored word the user can change to point 
to their own routine for reading 1K blocks into memory from storage.

*block-read-vector ( +--+ addr )* - This is the address of the vector
for block-read.  Save the xt of your word here.

*block-write ( addr blk# +--+ )* - This is a vectored word the user can change to point 
to their own routine for writing 1K blocks from memory to storage.

*block-write-vector ( +--+ addr )* - This is the address of the vector
for block-write.  Save the xt of your word here.

*block-ramdrive-init ( u +--+ )* - Create a RAM drive with the given number of
blocks (numbered 0 to (u-1)) to allow use of the block words with no additional
hardware.  Because the blocks are only held in RAM, they will be lost when the
hardware is powered down or the simulator is stopped.

*cleave ( addr u +--+ addr2 u2 addr1 u1 )* - Given a block of character memory
with words separated by whitespace, split off the first sub-block and put it in
TOS and NOS.  Leave the rest lower down on the stack. This allows breaking off
single words (or zero-terminated strings in memory, with a different delimiter)
for further processing. Use with loops:

----
        : tokenize ( addr u -- )
            begin
                cleave
                cr type  \ <-- processing of single word 
            dup 0= until
            2drop ; 
----

For a string such as `s" emergency induction port"`, this gives us:

----
        emergency
        induction
        port
----

The payload of such a loop can be modified to process any `( addr u )`. For
example, using the `execute-parsing` word, we can define a series of variables
at run time:

----
        : make-variables ( addr u -- )
            begin
                cleave
                ['] variable execute-parsing  \ <-- new function
            dup 0= until
            2drop ; 
----

Running `s" tali garrus joker shepard" make-variables` will define those four
words as variables, as `words` will show. More generally, we can use `cleave` to
create a version of the `map` higher-order function in Forth.

----
        : map ( addr u xt -- )
            >r
            begin
                cleave
                r@ execute  \ <-- must consume ( addr u )
            dup 0= until
            2drop  r> drop ;  
----

*compile-only ( +--+ )* - Mark last word in dictionary as compile-only.

*digit? ( char +--+ u f | char f )* - If character is a digit, convert and set flag to
`true`, otherwise return the offending character and a `false` flag.

*ed ( +--+ )* - Start the command-line editor. There is a whole chapter on this father
down. 

*hexstore ( addr u addr1 +--+ u2 )* - Store string of numbers in memory. Given a
string with numbers of the current base seperated by spaces, store the numbers
at the address `addr1`, returning the number of elements. Non-number elements
are skipped, an zero-length string produces a zero output. Use as a poor man's
assembler:

----
        hex  s" ca ca 95 00 74 01" myprog hexstore
        myprog swap execute
----

With this behavior, `hexstore` functions as a reverse `dump`. The names "store"
or "numberstore" might have been more appropriate, but "hexstore" as the
association of the Unix command `hexdump` and should be easier to understand.

*input ( +--+ )* - Return the address where the vector for the input routine is
stored (not the vector itself). Used for input redirection for
`emit` and others.

*input>r ( +--+ ) ( R: +--+ n n n n )* - Saves the current input state to the
Return Stack. This is used for `evaluate`. ANS Forth does provide the word
`save-input` (see https://forth-standard.org/standard/core/SAVE-INPUT), but it
pushes the state to the Data Stack, not the Return Stack. The reverse operation
is `r>input`.

*int>name ( xt +--+ nt )* - Given the execution execution token (xt)* -, return the
name token (nt)* -.

*latestnt ( +--+ nt )* - Return the last used name token. The Gforth version of this
 word is called `latest`.

*nc-limit ( +--+ addr )* - Return the address where the threshold value for native
compiling native compiling is kept. To check the value of this parameter, use `nc-limit ?`. 
The default value is 20.

*never-native ( +--+ )* - Mark most recent word so it is never natively compiled.

*number ( addr u +--+ u | d )* - Convert a string to a number.  Gforth uses
`s>number?` and returns a success flag as well.

*output ( +--+ addr )* - Return the address where the vector for the output routine is
stored (not the vector itself)* -. Used for output redirection for `emit` and
others.

*r>input ( +--+) ( R: n n n n +--+ )* - Restore input state from Return Stack.
See `input>r` for details.

*strip-underflow ( +--+ addr )* - Return the address where the flag is kept that decides
if the underflow checks are removed during native compiling. To check the value
of this flag, use `strip-underflow ?`.

*useraddr ( +--+ addr )* - Return the base address of the block of memory holding the
user variables.

*wordsize ( nt +--+ u )* - Given the name token (`nt`) of a Forth word, return
its size in bytes. Used to help tune native compiling. Note that `wordsize`
expects the name token (`nt`) of a word, not the execution token (`xt`). This
might be changed in future versions.

*-leading ( addr u +--+ addr1 u1 )* - Strip any leading whitespace. This is the
other side of the ANS Forth string word `-trailing`.

=== Wordlists and Search Order

Tali Forth implements the optional Search-Order words, including the extended
words.  These words can be used to hide certain words or to rearrange the order
the words are searched in, allowing configurable substitution in the case of
words that have the same name but live in different wordlists.

On startup, only the FORTH-WORDLIST is in the search order, so only those words
will be found.  Tali also comes with an EDITOR-WORDLIST and an
ASSEMBLER-WORDLIST, however those are not fully populated (mostly empty would be
a better description of the current situation).  Room for 8 user wordlists is
available, and the search order can also hold 8 wordlist identifiers. See
https://forth-standard.org/standard/search for more information on wordlists and
the search order.

The WORDLIST word will create a new wordlist (or print an error message if all 8
user wordlists have already been created).  It puts the wordlist identifer (wid)
on the stack.  This is simply a number that uniquely identifes the wordlist, and
it's common practice to give it a name rather than use the number directly.  An
example might look like:

----
wordlist constant MY-WORDLIST
----

While this creates a new wordlist and gives it a name, the wordlist isn't
currently set up to be used.  When Tali starts, only the FORTH-WORDLIST is set
up in the search order and all compilation of new words goes into the
FORTH-WORDLIST.  After creating a new wordlist, you need to set it up for new
words to be compiled to it using SET-CURRENT and you need to add it to the
search order using SET-ORDER if you want the new words to be found.

----
\ Set up the new wordlist as the current (compilation) wordlist
\ New words are always put in the current wordlist.
MY-WORDLIST set-current

\ Put this wordlist in the search order so it will be searched
\ before the FORTH-WORDLIST.  To set the search order, put the
\ wids on the stack in reverse order (last one listed is seached
\ first), then the number of wids, and then SET-ORDER.
FORTH-WORDLIST MY-WORDLIST 2 set-order

: new-word s" This word is in MY-WORDLIST"

\ Go back to compiling into the FORTH-WORDLIST.
FORTH-WORDLIST set-current
----

=== Native Compiling

As the name says, subroutine threaded code encodes the words as a series of
subroutine jumps. Because of the overhead caused by these jumps, this can make
the code slow. Therefore, Tali Forth enables native compiling, where the
machine code from the word itself is included instead of a subroutine jump. This
is also called "inlining".

The parameter `nc-limit` sets the limit of how small words have to be to be
natively compiled. To get the current value (usually 20), check the value of
the system variable:

----
nc-limit ?
----

To set a new limit, save the maximal allowed number of bytes in the machine
code like any other Forth variable:

----
40 nc-limit !
----

To completely turn off native compiling, set this value to zero.


=== Underflow Detection

When a word tries to access more words on the stack than it is holding, an
"underflow" error occurs. Whereas Tali Forth 1 didn't check
for these errors, this version does.

However, this slows the program down. Because of this, the user can turn off
underflow detection for words that are natively compiled into new words. To do
this, set the system variable `strip-underflow` to `true`. Note this does not
turn off underflow detection in the built-in words. Also, words with underflow
detection that are not included in new words through native compiling will also
retain their tests.

=== Restarting

Tali Forth has a non-standard word `cold` that resets the system. This doesn't
erase any data in memory, but just moves the pointers back. When in doubt, you
might be better off quitting and restarting completely.

=== Gotchas

Some things to look out for when using Tali Forth.

==== Cell Size

Tali has a 16-bit cell size. 

NOTE: Use `1 cells 8 * .` to get the cell size in bits with any Forth.

This can trip up calculations when compared to the _de facto_ standard Gforth
with 64 bits. Take this example:

----
( Gforth )      decimal 1000 100 um* hex swap u. u.  ( returns 186a0 0  ok )
( Tali Forth)   decimal 1000 100 um* hex swap u. u.  ( returns 86a0 1  ok ) 
----

Tali has to use the upper cell of a double-celled number to correctly report
the result, while Gforth doesn't. If the conversion from double to single is
only via a `drop` instruction, this will produce different results.

There is a similar effect with the Gforth word `bounds`: Because of Tali's 16
bit address space, it wraps the upper address if we go beyond $FFFF:

---- 
( Gforth )      hex FFFF 2 bounds  swap u. u.  ( returns 10001 ffff  ok  )
( Tali )        hex FFFF 2 bounds  swap u. u.  ( returns     1 ffff  ok )
----

==== Delimiters During Parsing

Both `parse-name` and `parse` skip white space - defined as ASCII characters
from 00 to 32 (SPACE) inclusive - when the standard talks about "spaces".
Otherwise, Tali would choke on TABs during compiling, and the `ed` editor
couldn't be used to edit programs because of the Line Feed characters. This is
covered in the standard, see the footnote at
https://forth-standard.org/standard/core/PARSE-NAME by Anton Ertl, referencing 
http://forth-standard.org/standard/usage#subsubsection.3.4.1.1 and
http://forth-standard.org/standard/file#subsection.11.3.5 .

==== Negative `allot`

The ANSI standard does not define what happens if there is an attempt to free
more memory with `allot` by passing a negative value than is available. Tali
will let the user free memory up the beginning of RAM assigned to the Dictionary
(marked with `cp0` in the code), even though this can mean that the Dictionary
itself is compromised. This is Forth, you're the boss.

However, any attempt to free more memory than that will set the beginning of RAM
to `cp0`. Also, the Dictionary Pointer `dp` will point to the _last native word_
of the Dictionary, which is usually `drop`. Because of this, the high level
words defined during boot will _not_ be available. There will be an error
message to document this. Realistically, you'll probably want to restart with
`cold` if any of this happens.


